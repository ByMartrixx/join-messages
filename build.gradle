plugins {
    id 'fabric-loom' version '0.7-SNAPSHOT'
    id 'maven-publish'
    id 'java-library'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

archivesBaseName = project.archives_base_name
group = project.maven_group

boolean isBuild = System.getenv('GITHUB_WORKFLOW') == 'build'
version = !isBuild ? project.mod_version : "${project.mod_version}+build.${System.getenv('GITHUB_RUN_NUMBER')}"

allprojects {
    apply plugin: 'fabric-loom'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        maven {
            name 'FabricMC'
            url 'https://maven.fabricmc.net'
        }
        maven {
            url 'https://maven.bymartrixx.me'
        }
    }

    dependencies {
        //to change the versions see the gradle.properties file
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"

        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

        testImplementation 'me.bymartrixx:wdmcf:1.0.2'
    }

    processResources {
        inputs.property 'version', rootProject.version

        filesMatching('fabric.mod.json') {
            expand 'version': rootProject.version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        // ensure that the encoding is set to UTF-8, no matter what the system default is
        // this fixes some edge cases with special characters not displaying correctly
        // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
        // If Javadoc is generated, this must be specified in that task too.
        it.options.encoding = 'UTF-8'

        // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
        // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
        // We'll use that if it's available, but otherwise we'll use the older option.
        def targetVersion = 8
        if (JavaVersion.current().isJava9Compatible()) {
            it.options.release = targetVersion
        }
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()
    }

    jar {
        from('LICENSE') {
            rename { "${it}_${project.archivesBaseName}" }
        }
    }
}

subprojects {
    apply plugin: 'fabric-loom'
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'

    archivesBaseName = project.name
    version = rootProject.version
}

repositories {
    maven {
        name 'JitPack'
        url 'https://jitpack.io'
    }
}

dependencies {
    // Add subprojects as dependency of root project
    api project(':api')
    include project(':api')

    // FabricPlaceholderApi
    modImplementation include('com.github.Patbox:FabricPlaceholderAPI:0.3.0')
}

subprojects.each {
    rootProject.remapJar.dependsOn(it.remapJar)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar){
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

curseforge {
    if (project.hasProperty('curse_api_key') || System.getenv('CURSE_API_KEY') != null) {
        apiKey = project.hasProperty('curse_api_key') ? project.property('curse_api_key') : System.getenv('CURSE_API_KEY')
    }
    project {
        id = '404348'
        changelog = 'A changelog can be found at https://github.com/ByMartrixx/player-events/releases/latest'
        releaseType = 'release'
        addGameVersion '1.16'
        addGameVersion '1.16.1'
        addGameVersion '1.16.2'
        addGameVersion '1.16.3'
        addGameVersion '1.16.4'
        addGameVersion '1.16.5'
        addGameVersion 'Fabric'
        mainArtifact(remapJar) {
            displayName = "Player Events ${version} ${project.valid_minecraft_version}"
        }
        addArtifact(project(':api').remapJar) {
            displayName = "Player Events API ${version} ${project.valid_minecraft_version}"
        }
        afterEvaluate {
            uploadTask.dependsOn('remapJar')
            uploadTask.dependsOn('api:remapJar')
        }
    }
    options {
        forgeGradleIntegration = false
    }
}
